S -> S 'nl' S1 | S1
S1 -> COND | LOOP | RAISE | RET | EXP | ASSIGN | IMPORT | CLASS | DEF | 'pass' | 'break' | 'cont' | 'comment'

ASSIGN -> ASSIGNCHAIN | 'id' ASSIGNOP EXP
ASSIGNCHAIN -> 'id' 'eq' ASSIGNCHAIN | 'id' 'eq' EXP
ASSIGNOP -> 'eq' | 'plus' 'eq' | 'min' 'eq' | 'mult' 'eq' | 'div' 'eq' | 'mod' 'eq' | 'div' 'div' 'eq' | 'mult' 'mult' 'eq' | 'amp' 'eq' | 'bor' 'eq' | 'bnot' 'eq' | 'gt' 'gt' 'eq' | 'lt' 'lt' 'eq'

EXP -> PAREXP | LOGICEXP | MATHEXP | FUNCEXP | 'id' LB EXP RB | DICT | TUPLE | LIT | 'id'

LIT -> REAL | 'int' | 'xbo' | 'str' | 'false' | 'true' | 'none'
REAL -> 'int' 'dot' 'int' | 'dot' 'int'

DICT -> LC DICTLIST RC
DICTLIST -> DICTLIST DICT1 | DICT1
DICT1 -> 'str' COLON EXP COMMA
TUPLE -> LP ARGS RP
ARGS -> ARGS1 | ε
ARGS1 -> ARGS1 COMMA EXP | EXP

FUNCEXP -> 'id' TUPLE
COMMA -> 'comma' NL

PAREXP -> LP PAREXP1 RP
PAREXP1 -> EXP | PARLOGICEXP | PARMATHEXP
PARLOGICEXP -> EXP NL RELOP NL EXP | 'not' EXP | LOGICEXP
PARMATHEXP -> EXP NL MATHOP2 NL EXP | MATHEXP

LOGICEXP -> EXP RELOP EXP | 'not' EXP
RELOP -> 'and' | 'or' | 'is' | 'is' 'not' | 'in' | 'not' 'in' | 'eq' 'eq' | 'exc' 'eq' | 'gt' | 'gt' 'eq' | 'lt' | 'lt' 'eq'

MATHEXP -> EXP MATHOP2 EXP | MATHOP1 EXP
MATHOP1 -> 'plus' | 'min' | 'tilde'
MATHOP2 -> 'mult' | 'mult' 'mult' | 'div' | 'div' 'div' | 'mod' | 'plus' | 'min' | 'lt' 'lt' | 'gt' 'gt' | 'amp' | 'bnot' | 'bor'

IMPORT -> 'from' FROMPKG IMPORT | 'import' PKG 'as' 'id' | 'import' PKG
FROMPKG -> 'dot' PKG | 'dot' 'dot' PKG | 'dot'
PKG -> 'id' 'dot' PKG | 'id'

COND -> 'if' EXP COLON S CONDTAIL
COLON -> 'colon' NL
CONDTAIL -> 'elif' EXP COLON S CONDTAIL | CONDTAIL 'else' COLON S | ε

LOOP -> 'for' EXP 'in' EXP COLON S | 'while' EXP COLON S

WITH -> 'with' EXP 'as' 'id' COLON S

RAISE -> RAISE 'from' 'id' | 'raise' 'id' | 'raise' FUNCEXP
RET -> 'return' EXP | 'return'

DEF -> 'def' 'id' LP DEFARGS RP DEFRETT COLON S
DEFARGS -> DEFARGS1 | ε
DEFARGS1 -> DEFARGS1 COMMA 'id' | 'id'
DEFARG -> 'id' COLON ARGTYPE | 'id'
ARGTYPE -> LP ARGTYPE RP | 'id' LB ARGTYPE RB | 'id'
DEFRETT -> 'min' 'gt' 'id' | ε

CLASS -> 'class' 'id' COLON S

LC -> 'lc' NL
RC -> NL 'rc'
LB -> 'lb' NL
RB -> NL 'rb'
LP -> 'lp' NL
RP -> NL 'rp'
NL -> 'nl' | ε
